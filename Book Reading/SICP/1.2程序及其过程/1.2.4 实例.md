#SICP 
# 求幂Exponentiation
- 计算 $b^n$，有如下递归定义：
$$ b^n= \begin{cases} 1 & \text {if n=0,} \\ b \cdot b^{n-1} & \text{if n>0} \end{cases} $$

- 翻译为如下递归过程：
	```scheme
	(define (expt b n)
	  (if (= n 0)
		  1
		  (* b (expt b (- n 1)))))
	```
- **线性递归计算过程**，时间和空间消耗都为 $\Theta(n)$;
- 根据迭代和递归[[1.2 程序及其所产生的过程#相互转化|相互转化提示]]转化为迭代过程：
	```scheme
	(define (expt b n)
	  (expt-iter b n 1))
	(define (expt-iter b counter product)
	  (if (= counter 0)
		  product
		  (expt-iter b
					(- counter 1)
					(* b product))))
	```
- **线性迭代计算过程**，时间和空间消耗分别为 $\Theta(n)$ 和 $\Theta(1)$。

## 求幂的优化
**提高计算的基底**
- 如$b^8$ 原始计算过程为 b·(b·(b·(b·(b·(b·(b·b))))))；
- 基底用 $b^2$ 而不是 b :
	- $b^2=b\cdot b$
	- $b^4=b^2 \cdot b^2$
	- $b^8=b^4 \cdot b^4$
- 一般有： $$b^n= \begin{cases} (b^{n/2})^2 & \text {n 是偶数,} \\ b \cdot b^{(n-1)/2} & \text{n 是奇数} \end{cases}$$
- 其中 n 是奇数后，n-1为偶数可以继续应用偶数公式。
- 空间和时间消耗是**以2为底的对数（计算规模 n 只需要 $\log n$ 次），$\Theta(\log n)$**
- 计算 process `fast-expt` 如下：
	```scheme
	(define (fast-expt b n)
	  (cond ((= n 0) 1)
			((even? n) (square (fast-expt b (/ n 2))))
			(else (* b (fast-expt b (- n 1))))))
	``` ^e9996e
- 判断n是否是偶数的 process `even?`：
	```scheme
	(define (even? n)
	  (= (remainder n 2) 0))
	```
- **remainder 是取余操作，是基本过程（primitive procedure）**。


# 最大公约数Greatest Common Divisors
- 两个整数 a 和 b 的*最大公约数（GCD）*是*能除尽这两个数的最大整数*；
- 欧几里得算法：*a 和 b 的公约数等于 b 和 r （r 是 a 除以 b 的余数）的公约数*：$GCD(a,b)=GCD(b,r)$，其中 r = a%b;
- 把一个GCD的计算问题连续地**归约**到越来越小的GCD计算问题：
*GCD(206,40) = GCD(40,6) 
                              = GCD(6,4) 
							  = GCD(4,2) 
							  = GCD(2,0) 
							  = 2*
- 翻译为如下 process `gcd`：
	```scheme
	(define (gcd a b)
	  (if (= b 0)
		  a
		  (gcd b (remainder a b))))
	```

- 空间和时间消耗与规模为*对数关系*。

# 素数检测Testing for Primality
**素数（prime numbers）**：大于1，除了1和自身没有其他因数的自然数。
## 寻找因子
- 定理：**如果 n 不是素数，它必然有一个小于等于$\sqrt{n}$的因子**;
- 检测素数：从 2 开始找出它的因子；
- 如果存在任意小于 n 的因子，则不是素数，否则（最小因子为 n）是素数；
- 有如下 process `smallest-divisor`
	```scheme
	#|从2开始查找最小因子|#
	(define (smallest-divisor n)
	  (find-divisor n 2))

	#|依据定理查找因子|#
	(define (find-divisor n test-divisor)
	  (cond ((> (square test-divisor) n) n)
			((divides? test-divisor n) test-divisor)
			(else (find-divisor n (+ test-divisor 1)))))

	#|是否能整除|#
	(define (divides? a b)
	  (= (remainder b a) 0))
	```

- *当且仅当 n 是自己的最小因子时，n 为素数*
	```scheme
	(define (prime? n)
	  (= n (smallest-divisor n)))
	```

1. 该算法只需在1和$\sqrt{n}$之间检查因子。
2. ** 时间消耗将具有 $\Theta(\sqrt(n))$ 的增长阶**；

## 费马检查The Fermat test
**费马小定理（Fermat’s Little Theorem）**：**如果 n 是一个素数，a是小于 n 的任意正整数，那么 $a^n$ 与a模n同余**；
- **模n同余（congruent modulo n）**：a 与 b 对 n 取模，余数相同。
- 检查素数算法：
	- 对于给定的整数 n，随机任取一个 \[1, n-1] 并计算出 $a^n$ 模n的余数。
	- 如果余数不等于a，那么n肯定不是素数。
	- 如果余数等于 a，那么n是素数可能性增大。
	- 通过检查越来越多的a值，可以不断增加对有关结果的信心。

计算一个数的幂对另一个数取模 process：
```scheme
#|求base的exp次幂对m取模的结果|#
(define (expmod base exp m)
  (cond ((= exp 0) 1)
        ((even? exp)
         (remainder (square (expmod base (/ exp 2) m))
                    m))
        (else
         (remainder (* base (expmod base (- exp 1) m))
                    m))))
```
- 类似于[[#求幂的优化|fast-expt]] process，采用*连续求平方*的方式，*时间消耗为对数*；
- 随机数 a 的选取通过 `random` process，`((random n-1) +1)`得到\[1, n-1]随机数;
- `fermat-test`:
	```scheme
	(define (fermat-test n)
	  (define (try-it a)
		(= (expmod a n n) a))
	  (try-it (+ 1 (random (- n 1)))))
	```

- 基于 fermat test 的快速判断素数的 process `fast-prime?`：
	 ```scheme
	(define (fast-prime? n times)
	  (cond ((= times 0) true)
			((fermat-test n) (fast-prime? n (- times 1)))
			(else false)))
	```

### 特点 

- 费马检查与前面的算法不一样。前面算法都**保证计算结果的正确性**，而费马检查的结果**只有概率上的正确性**；
- 费马检查是一种**概率算法**；
- 如果数 n 不能通过费马检查，可以*确信它一定不是素数*。
- 如果 n 通过了费马检查，只能说 *n 很可能是素数*，但却不能保证。
- 存在着一些**能骗过费马检查的非素数**，但这种数极其罕见，因此*费马检查在实践中还是很可靠的*。
- 可以证明，对任意的数 n，费马检查对整数 a＜n 大部分都不成立，除非 n 是素数。
- 所以*如果 n 对某个随机整数 a 能通过检查，n 是素数的几率大于1/2*。通过用更多随机 a 值，可以**使出错的概率减小到所需要的任意程度**。