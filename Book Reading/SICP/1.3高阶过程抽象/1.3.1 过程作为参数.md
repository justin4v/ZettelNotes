#SICP 
# Procedures as Arguments
有如下三个过程
1. 计算从a到b的各整数之和 `sum-integers`
```scheme
(define (sum-integers a b)
  (if (> a b)
      0
      (+ a (sum-integers (+ a 1) b))))
```

2. 计算a到b整数的立方之和 `sum-cubes`
```scheme
(define (sum-cubes a b)
  (if (> a b)
      0
      (+ (cube a) (sum-cubes (+ a 1) b))))
```

3. 计算下面的序列之和 `pi-sum`
$$\frac{1}{1 \cdot 3 } + \frac{1}{ 5 \cdot 7}+\frac{1}{9 \cdot 11}+..... $$
```scheme
(define (pi-sum a b)
  (if (> a b)
      0
      (+ (/ 1.0 (* a (+ a 2))) (pi-sum (+ a 4) b))))
```

- 上述三个过程有一种*公共的模式*：**求一系列数的和**，有如下的**公共 `sum` 过程**：
	```scheme
	(define (sum term a next b)
	  (if (> a b)
		  0
		  (+ (term a)
			 (sum term (next a) next b))))
	```

- 数学中使用求和符号 $\Sigma$ 表达**求和的概念**，而不是某个特定的求和：$$\sum_{n=a}^bf(n) = f(a)+...+f(b)$$

- 用模板过程定义 `sum-cubes`:
	```scheme
	(define (inc n) (+ n 1))
	(define (sum-cubes a b)
	  (sum cube a inc b))
	```

## 构建其他概念
- 有了求和基本概念描述 process `sum`，可用它作为*基本构件*，去*形式化（formulating）其他概念*。

例如，求出函数f在a和b之间的定积分的近似值，有如下公式：
$$\int_a^bf=[f(a+\frac{dx}{2})+f(a+dx+\frac{dx}{2})+f(a+2dx+\frac{dx}{2})+...]dx$$

- 其中 dx 是一个很小的*增量*：
	```scheme
	(define (integral f a b dx)
	  (define (add-dx x) (+ x dx))
	  (* (sum f (+ a (/ dx 2.0)) add-dx b)
		 dx))
	
	#|求值|#
	(integral cube 0 1 0.01)
	.24998750000000042
	(integral cube 0 1 0.001)
	.249999875000001
	```


