#SICP 

# Procedures as General Methods
- 前面介绍了[[1.1.4 复合程序|复合程序 Compound Procedure]]：将**多个操作的模式抽象出来**，使所描述的计算程序不再依赖于所涉及的特定数值。
- [[1.3.1 程序作为参数|高阶程序 Higher-order Procedure]]：一种更强大的抽象，*参数为另一个 procedure*，可用于**表述计算的一般性过程**，与所涉及的特定函数无关。

下面将讨论两个实例：找出函数零点和不动点的一般性方法，说明*如何通过程序(procedures)去直接表述这些方法*。

## 示例1 通过区间二分法寻找方程的根
- **区间二分法（half-interval method）** 是寻找方程 f(x)＝0 根的一种简单而又强有力的方法， *f 是连续函数*；
- 基本思想：
	1. 给定区间 [a,b] 有 f(a)＜0＜f(b)，则 f 在 a 和 b 之间必然有一个零点。
	2. 令 $x=(a+b)/2$ ，计算出 f(x)。
	3. 如果 f(x)＞0，那么零点在 [a, x]；
	4. 如果 f(x)＜0，那么零点在 [x, b];
	5. 重复步骤 2-4，直到区间大小满足精度要求。
	6. 步数的*增长是 $\Theta(log(L/T))$*，其中L是区间的初始长度，T是要求的区间精度大小。
- 这一策略*用 Procedure 直接表述*：
	```scheme
	#f#
	(define (search f neg-point pos-point)
	  (let ((midpoint (average neg-point pos-point)))
	    (if (close-enough? neg-point pos-point)
	        midpoint
			#else 就执行如下语句#
	        (let ((test-value (f midpoint)))
			  #let语句 的 body#
	          (cond ((positive? test-value)
	                 (search f neg-point midpoint))
	                ((negative? test-value)
	                 (search f midpoint pos-point))
	                (else midpoint))))))
	```
	- 假定给定了函数 f，以及使它取值为负和正的neg-point、pos-point 的两个点。
	- 首先算出中点 midpoint，而后检查给定区间是否已经足够小。
	- 如果是，就返回 midpoint 的值；
	- 否则算出 f 在这个中点的值 test-value。
	- 如果test-value为正，那么(search f neg-point midpoint)；
	- 如果test-value为负，就(search f midpoint pos-point)。
	- 如果test-value = 0，这时 midpoint 就是所寻找的根。


检查两个端点是否“足够接近”：
```scheme
(define (close-enough? x y)
  (< (abs (- x y)) 0.001))
```

### 注意
- *search procedure 很难直接使用*，因为可能输入的 neg-point 和 pos-point，相应的 f 值并不具有所需的正负号；
- 在使用 *search* 前*首先判断是否满足端点正负号的条件*，procedure 如下：
	```scheme
	#是一个 Higher-order procedure#
	(define (half-interval-method f a b)
	  
	  (let ((a-value (f a))
	        (b-value (f b)))
		#判断是否满足端点条件，然后调用 search#
	    (cond ((and (negative? a-value) (positive? b-value))
	           (search f a b))
	          ((and (negative? b-value) (positive? a-value))
	           (search f b a))
	          (else
	           (error "Values are not of opposite sign" a b)))))
	```

### 调用示例
用 *search* 找出 $x^3-2x-3=0$ 在[1, 2]的根：

```scheme
#用 lambda 定义匿名 procedure#
(half-interval-method (lambda (x) (- (* x x x) (* 2 x) 3))
                      1.0
                      2.0)
1.89306640625
```

## 示例2 找出函数不动点

-  **满足方程 $f(x)＝x$ 的点 x 称为函数 $f(x)$ 的*不动点(fixed points)***

- 求**不动点流程**：
	1. 对于函数 $f$，从**初始猜测 $x_0$** 出发；
	2. 应用 $f$ 于 $x_0$ 得到 $f(x_0)$，**计算 $f(x_0)-x_0$ 差值，检查是否瞒住精度要求**；
	3. 如果满足要求，返回 $x_0$；
	4. 否则，将 $f(x_0)$ 设为 $x_1$并作为**下一个猜测值(next guess)**；
	5. 重复步骤 2-4，直到 $x_n$ （实际上是$\underbrace{f(f(f(\cdots f(x_0))))}_{n}$）和 $f(x_n)$ 差值满足要求。
	6. 实际是反复应用 procedure $f$：$f(x), f( f(x)), f( f( f(x))), . . .$ 。**前面一个**如 $f(f(x))$**实际上看做是一个 x 的猜测值**，**后面** $f(f(f(x)))$ **实际上是对猜测值应用 $f$ 得到的值**。
- 根据上述思路，设计出 *fixed-point* procedure：
	```scheme
	(define tolerance 0.00001)
	
	(define (fixed-point f first-guess)
	  
	  (define (close-enough? v1 v2)
	    (< (abs (- v1 v2)) tolerance))
	  #|定义内部 procedure|#
	  (define (try guess)
	    (let ((next (f guess)))
	      (if (close-enough? guess next)
	          next
	          (try next))))
	  
	  (try first-guess))
	```


### 示例
求出余弦函数的不动点，其中用 1 作为初始近似值：

```scheme
(fixed-point cos 1.0)
.7390822985224023
```

找出方程 y＝sin y＋cos y 的一个解：
```scheme
(fixed-point (lambda (y) (+ (sin y) (cos y)))
             1.0)
1.2587315962971173
```

### 联系
- 不动点的计算过程和前面[[1.1.7 过程作为黑箱抽象#牛顿法求平方根|计算平方根]]类似。
- 两者基于*同样的思想*：**通过不断地改进猜测，直至结果满足某一评价准则为止**。
- 可以将平方根计算形式化(formulate) 为寻找不动点的计算过程：
	- 计算某个数 x 的平方根，也就是找到一个 y 使得 $y^2＝x$ ，等价于求解 $y＝x/y$；
	- *实际就是寻找函数$f(y)=x/y$ 的不动点*。
	- 因此，可以用下面方式试着去计算平方根：
	```scheme
	(define (sqrt x)
	  (fixed-point (lambda (y) (/ x y))
	               1.0))
	```
	- 遗憾的是，这一不动点搜寻**并不收敛**。
		- 考虑某个初始猜测 $y_1$;
		- 下一个猜测是$y_2=x/y_1$;
		- 而再下一个猜测是 $y_3＝x/y_2＝x/(x/y_1)＝y_1$。
		- 结果进入了无限循环，*猜测值在$y_1$和$y_2$振荡（oscillating）*。

#### 平均阻尼
- 控制这类振荡的一种方法是**不让猜测与猜测之间变化太剧烈**。
- 因为猜测是在 $y$ 和 $x/y$ 之间振荡，可以*调整 next 的计算方式，让其不像$x/y$ 一样与$y$ 相差很大* ，控制猜测的变化程度。
- 可以用 $y$ 和 $x/y$ 的平均值计算 next 猜测。next 为 $\frac{1}{2}(y+x/y)$ 而不是$x/y$;
- 将 $y＝x/y$ 变换为 $2y＝x/y+y$ ，进一步变换为 $y＝\frac{1}{2}(y+x/y)$，所以有：

```scheme
(define (sqrt x)
  (fixed-point (lambda (y) (average y (/ x y)))
               1.0))
```

- 事实上，*sqrt* 求平方根时产生的近似值序列，就是[[1.1.7 过程作为黑箱抽象#牛顿法求平方根|牛顿法求平方根]]产生的序列。
- 这种*通过平均值逐次逼近*，直到找到解的技术，称为**平均阻尼(average damping)** 的技术，**常用在不动点搜寻**中，作为帮助收敛的手段。