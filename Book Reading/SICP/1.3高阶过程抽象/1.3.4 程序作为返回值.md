#SICP 
# Procedures as Returned Values
[[1.3.3 程序作为一般性方法#示例1 通过区间二分法寻找方程的根|二分法求根]] 和 [[1.3.3 程序作为一般性方法#示例2 找出函数不动点|求函数不动点]] 示例说明：
- **程序作为参数传递**，能显著*增强语言的表达能力(expressive power)*。

*程序作为返回值返回*，将能得到进一步的表达能力。

- 在 [[1.3.3 程序作为一般性方法#示例2 找出函数不动点|求函数不动点]] 示例中，平方根程序（square-root）的新版本：将平方根计算看作不动点搜寻过程。
- 而后用**平均阻尼（average damping）** 使得不动点搜寻的逼近**收敛**。
- *平均阻尼*：不动点搜寻中，给定函数 f ；设有另一函数，它在 x 处的值等于x和  $f(x)$ 的平均值。这个函数就是不动点搜寻中，f 的平均阻尼函数。有如下程序：
	```scheme
	(define (average-damp f)
	  (lambda (x) (average x (f x))))
	```
	1. `average-damp` 是一个 higher-order peocedure;
	2. 参数是一个 procedure f;
	3. 返回值是另一个 procedure（ `lambda`产生），当应用于 x 时，返回 x 与 $f(x)$ 的均值;


将 `average-damp` 应用于 `square`，表示另一个 procedure，在数值 x 处的值是 x 和 $x^2$ 的均值:
```scheme
((average-damp square) 10)
55
```

利用 `average-damp`，前面的 [[1.3.3 程序作为一般性方法#示例2 找出函数不动点|求函数不动点]] 中平方根 procedure 可表达如下：

```scheme
(define (sqrt x)
  (fixed-point (average-damp (lambda (y) (/ x y)))
               1.0))
```
- 上述 `sqrt` procedure 将*不动点搜寻(fixed-point search)*，*平均阻尼(average damping)* 和 *函数 $y \mapsto x/y$* 结合在一起;
- `sqrt` procedure 可与 [[1.1.7 过程作为黑箱抽象#^0ff3bb|牛顿法求平方根]] 对比，注意如何用 fixed-point 、average damping等抽象描述 `sqrt` 概念，让`sqrt` procedure 更加清晰。

## 注意
- 将一个 process *形式化(formulate)* 为一个 procedure，一般存在多种方式；
- 选择程序的形式(procedural formulations)，应该使概念清晰且易理解；
- 计算过程中的元素能相互分离，便于重用。

例如，立方根是函数 $y\mapsto x/y^2$ 的不动点，将平方根计算过程推广为立方根计算过程：
```scheme
(define (cube-root x)
  (fixed-point (average-damp (lambda (y) (/ x (square y))))
               1.0))
```



# 牛顿法
**牛顿法(newtons-method)** 有一个特殊情况：
- 如果 $x \mapsto g(x)$ 是一个可微函数，方程 g(x)＝ 0 的一个解就是函数$x \mapsto f(x)$ 的一个不动点;
- 其中 $f(x)$ 为：
$$f(x)=x-\frac{g(x)}{Dg(x)}$$
- $Dg(x)$ 是 $g(x)$对 $x$ 的导数。
- 牛顿法就是用不动点方法，通过搜寻函数 f 的不动点的方式，得到方程g(x)＝ 0的解。

将牛顿法描述为 Procedure，首先要描述导数的概念：

## 导数
- “导数”是从函数到函数的一种变换；
- 描述的是 g(x) 在某一点 $x_0$ 处的变化率；
- 如果g是一个函数而dx是一个很小的数，那么g的导数在任意点 $x_0$ 处的值为：
$$Dg(x_0)=\frac{g(x_0 + dx)-g(x_0)}{dx}$$
- 用 procedure 描述：
```scheme
(define (deriv g)
  (lambda (x) (/ (- (g (+ x dx)) (g x)) dx)))

(define dx 0.00001)
```
- `deriv`以 procedure 为参数，并且返回一个 procedure；

例如，求 $x \mapsto x^3$ 在 5 处的导数：
```scheme
(define (cube x) (* x x x))
((deriv cube) 5)
75.00014999664018
```

## Procedure 描述牛顿法
- 牛顿法描述为一个*求不动点*的 Procedure：
	```scheme
	(define (newton-transform g)
	  (lambda (x)
	    (- x (/ (g x) ((deriv g) x)))))
	(define (newtons-method g guess)
	  (fixed-point (newton-transform g) guess))
	```

- 为确定 $x_0$的平方根，可用初始猜测1，通过牛顿法得到函数$y \mapsto y^2-x_0$的零点。
- 得到求平方根的另一种形式：
```scheme
(define (sqrt x0)
  (newtons-method (lambda (y) (- (square y) x0))
                  1.0))
```

# 抽象和一级程序
*Abstractions and first-class procedures*：
- 有两种方式计算平方根的方式：[[1.3.3 程序作为一般性方法#联系|不动点搜寻]]，[[#Procedure 描述牛顿法|牛顿法]]。
- 牛顿法实际描述的也是一个不动点的计算过程，所以实际上是不动点计算平方根的*两种形式*。
- 两种都是**从一个函数出发，找出函数在某种变换下的不动点**。
- 将这一具有*普遍性的思想表述为一个一般Procedure*：
	```scheme
	(define (fixed-point-of-transform g transform guess)
	  (fixed-point (transform g) guess))
	```
	- g 为待求不动点的函数；
	- transform 是变换形式的描述，用于对 g 进行变换；
	- guess 是初始猜测值。

[[1.3.3 程序作为一般性方法#平均阻尼|不动点搜寻计算平方根]]( 平均阻尼逼近 $y \mapsto x/y$ )作为上述一般性方法的实例(`transform` 为 `average-damp`)：
```scheme
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (/ x y))
                            average-damp
                            1.0))
```

[[#Procedure 描述牛顿法|牛顿法计算平方根]]（$y \mapsto y^2 - x_0$ 得到零点）作为上述一般性方法的实例(`transform` 为 `newton-transform`):
```scheme
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (- (square y) x))
                            newton-transform
                            1.0))
```


 - [[1.1.4 复合程序|复合程序]]，是一种重要的抽象机制，通过语言中的复合过程*能明确描述一般性的计算方法*；
 - [[1.3 用高阶程序做抽象|高阶程序]]能*操作这些一般性的方法(以其为参数)*，以便建立起进一步的抽象。


作为编程者：
- *设法识别出程序里的基本抽象(概念)*，基于它们去进一步构造更加强大的抽象。
- *基于抽象去思考*是最重要的，这样才可能在新的上下文中去应用它们。
- 高阶过程的重要性，在于：
	- 能**显式地用程序语言的要素描述抽象概念**；
	- 能**像操作其他计算元素一样去操作它们**。  


- 一般而言，程序设计语言*总会对计算元素的可能使用方式加上某些限制*。
- *带有最少限制的元素被称为具有第一级(first-class)的状态*。
- 第一级元素(first-class element)的某些“权利或者特权”包括：
	1. **可以用变量命名**；
	2. **可以作为 Procedure 的参数**；
	3. **可以作为 Procedure 的结果返回**；
	4. **可以包含在数据结构中**。