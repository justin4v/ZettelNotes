# Procedures as Black-Box Abstractions
## 牛顿法求平方根
牛顿逐步逼近法： ^280ea4
1. 如果对 x 的平方根有一个猜测 y；
2. 下一个更加逼近实际平方根的猜测为：( y + x/y )/2;
3. 像这样逐步逼近直到满足要求的精度。

1.可以得到如下的过程：
```scheme
(define (sqrt-iter guess x)
		(if (good-enough? guess x)
			guess
			(sqrt-iter (improve guess x) x)))
```

^0ff3bb

2.其中，改进猜测值的过程就是求平均值：
```scheme
(define (improve guess x) (average guess (/ x guess)))
```

3.其中
```scheme
(define (average x y) (/ (+ x y) 2))
```

4.此外，精度的计算这里简单用猜测值与实际值的差值在一定范围内（实际应该用猜测值变化的梯度--导数判断）：
```scheme
(define (good-enough? guess x)
		(< (abs (- (square guess) x)) 0.001))
```

^c1f57b

5.最后，使用 `1` 作为猜测值的起点。

## 过程抽象
1. *sqrt-iter* 的定义是递归的。
2. 其中用到的 `square` `abs` 等过程，*无需关心细节，只需要知道可以得到平方、绝对值结果*；
3. square 就是一个*黑箱*，可以认为是一个*过程的抽象（procedural abstraction）*；
4. 可以看做一个实际过程的*占位符*（参考：![[面向对象#^c906d6]]），实际实现上可以用任何能计算出平方的过程替换。

## Local Name
过程中用户不需要关心的细节之一就是过程中*形式参数（formal parameters）的名称*。
原则：
- *The meaning of a procedure should be independent of the parameter names used by its author*；
- **过程的意义不依赖于作者为形式参数选定的名称**。
- 所以：**过程的形式参数名必须局部于过程体**。

### bound / free variable
- **过程中的形式参数名被约束与过程中，这样的形式参数称为约束/绑定变量(bound variable)**，*需要首先形式化定义/传入，才可在过程内部使用*; ^9bc674
- 相反，*不被局限于过程*中的变量为**自由变量(free variable)**，*可以直接在过程内部使用*。 ^0c6220
- 一个名字的定义被约束于的表达式范围称为该名字的**作用域(scope)**。

例如在：
![[#^c1f57b]]
1. guess 和 x 是 bound variable；
2. <、-、 abs、 square是自由的；
3. guess 和 x 的变量名选择与 good-enough 意义无关，只要不与自由变量相同即可（如果相同，会将一个自由的变量变为约束的）。


## Internal definitions and block structure
一开始的分离过程：
```scheme
(define (sqrt x) (sqrt-iter 1.0 x))

(define (sqrt-iter guess x)
	(if (good-enough? guess x)
		guess 
		(sqrt-iter (improve guess x) x)))
		
(define (good-enough? guess x)
		(< (abs (- (square guess) x)) 0.001))
		
(define (improve guess x)
		(average guess (/ x guess)))
```

再整个过程中，用户其实只关心 `(sqrt x)` 过程，其他过程只是一种冗余的干扰。

### 块结构
将子过程局部化，定义到内部：
```scheme
(define (sqrt x)
		(define (good-enough? guess x)
			(< (abs (- (square guess) x)) 0.001))
		(define (improve guess x) (average guess (/ x guess)))
		(define (sqrt-iter guess x)
			(if (good-enough? guess x)
				guess
				(sqrt-iter (improve guess x) x)))
		(sqrt-iter 1.0 x))
```

- 这种嵌套定义（称为*块结构-block structure*）是最简单的*名称包装问题（name-packaging）* 的基本解决方案。
- 块结构可将辅助过程定义放入内部；
- 还可以简化内部过程：
	-  `sqrt` 中，`x` 是[[#^9bc674|绑定变量]]，作用域为 `sqrt`。
	-  由于 `good-enough?`, `improve guess x`, `sqrt-iter guess x` 也是定义于 `sqrt` 内部。
	-  `x` 可作为内部过程的[[#^0c6220|自由变量]]，不需要传入内部过程，内部过程即可直接使用。
	-  有如下简化后结果：
	```scheme 
	(define (sqrt x)
			(define (good-enough? guess)
					(< (abs (- (square guess) x)) 0.001))
			(define (improve guess)
					(average guess (/ x guess)))
			(define (sqrt-iter guess)
					(if (good-enough? guess)
					guess
					(sqrt-iter (improve guess))))
			(sqrt-iter 1.0))
	```
	在外部调用 `（sqrt x）` 过程时，`x` **由实际参数得到自己的值**，这种方式称为**词法作用域(lexical scoping)**：**过程中的自由变量实际引用了外围过程中定义中的约束（bindings）**。