#SICP #Abstraction-with-Data 
# 数据抽象
- **数据抽象（data abstraction）** 是一种*方法学(methodology)*，能*将复合数据对象的使用，与怎样由更基本的数据对象构造（ representation ）的细节隔离开*。  
- **潜在的想法(underlying idea)**：
	- *用一组基础操作标识一类数据对象*，使得**对数据的操作都可以基于基础操作表述**，在**操作数据时也只使用基础操作**。 ^cbe458
	- 程序使用数据时，*除了完成当前工作所需之外，不对数据做任何多余的假设*。
- *“具体”数据表示（ representation ）* 的定义(definition)，也*与程序中使用（use）数据的方式无关*。
- ***数据的表示和使用之间的接口***是一组 Procedure：**选择器(selectors)和构造器(constructors)**，在***具体的表示之上实现抽象数据 ( implement the abstract data in terms of the concrete representation )***。


# 实例：有理数的算术运算
假定需要做*有理数(Rational Numbers)*的加减乘除运算、比较是否相等等等。
- 有理数运算规律：$$\begin{align}
\frac {n_1} {d_1}+\frac {n_2} {d_2}=\frac {n_1d_2+n_2d_1} {d_1d_2} \\
\frac {n_1} {d_1}-\frac {n_2} {d_2}=\frac {n_1d_2-n_2d_1} {d_1d_2} \\
\frac {n_1} {d_1}·\frac {n_2} {d_2}=\frac {n_1n_2} {d_1d_2} \\
\frac {n_1/d_1} {n_2/d_2}=\frac {n_1d_2} {d_1n_2}\\
\frac {n_1} {d_1}=\frac {n_2} {d_2} 当且仅当 n_1d_2=n_2d_1
\end{align}$$
- 假定已经有从分子和分母*构造有理数的方法*；
- 假定已经有从有理数*取得(选择)分子和分母的方法*；
- 假定构造函数和选择函数都可作为 procedure 使用:
	1.  `(make-rat ⟨n⟩ ⟨d⟩)`：返回一个有理数，其*分子(numerator)* `⟨n⟩` 是整数，*分母(denominator)* `⟨d⟩` 是整数。
	2. `(numer ⟨x⟩)`：返回有理数 `⟨x⟩` 分子。
	3.  `(denom ⟨x⟩)` ：返回有理数 `⟨x⟩` 的分母
- 使用了称为***按愿望思维(wishful thinking)*** 的策略：*先不考虑*有理数如何表示、procedure `numer`、`denom`和 `make-rat`如何实现，*而是假设(wish)都已经实现*，则有如下 procedure：
	```scheme
	(define (add-rat x y)
	  (make-rat (+ (* (numer x) (denom y))
	               (* (numer y) (denom x)))
	            (* (denom x) (denom y))))
	
	(define (sub-rat x y)
	  (make-rat (- (* (numer x) (denom y))
	               (* (numer y) (denom x)))
	            (* (denom x) (denom y))))
	
	(define (mul-rat x y)
	  (make-rat (* (numer x) (numer y))
	            (* (denom x) (denom y))))
	
	(define (div-rat x y)
	  (make-rat (* (numer x) (denom y))
	            (* (denom x) (numer y))))
	
	(define (equal-rat? x y)
	  (= (* (numer x) (denom y))
	     (* (numer y) (denom x))))
	```
- 定义了在*选择和构造器* procedure `numer`、`denom`和`make-rat`*基础之上的各种有理数运算*，只是基础还没有定义。
- 需要“*粘合剂(glue)*”，将分子分母粘接起来，构成一个有理数。

## Pairs
- 为了具体实现有理数的数据抽象，Lisp 提供了**序对(pairs)**；
- **构造器(constructors)**：基本程序 `cons` ：输入两个参数，返回一个包含这两个参数的复合数据对象。
- **选择器(selectors)**：基本程序 `car`和`cdr`，分别返回 pairs 的第一个元素和剩余元素。
	```scheme
	(define x (cons 1 2))
	#|序对的第一个元素|#
	(car x)
	1
	#|序对的剩余元素|#
	(cdr x)
	2
	```
- *pairs 也是一个数据对象*，可以像*基本数据对象(primitive data object)* 一样赋予名字和操作。如用 `cons` 构造 pairs：
	```scheme
	(define x (cons 1 2))
	(define y (cons 3 4))
	
	#|cons 以pairs 为参数构造 pairs|#
	(define z (cons x y))
	(car (car z))
	1
	(car (cdr z))
	3
	```
- 序对(pairs)可像 primitive data object 一样操作表明：其*可用作构造任意类型复杂数据结构*的**通用基本构件(general-purpose building blocks)**。
- *由 `cons`、`car`和`cdr`实现的最基本的复合数据 Pairs*，就是要找的 *glue*。
- 由 Pairs 构造起来的数据对象称为*表结构数据 ( list-structured data )*，也是 *Lisp 语言名称的来由*。

## 有理数的表示
Pairs 有理数的表示提供了一种自然方式：
- *两个整数（分子和分母）组成的 Pairs* 表示有理数；
- 有了具体 representation 方案之后，`make-rat`、`numer`和`denom`的实现：
	```scheme
	(define (make-rat n d) (cons n d))
	(define (numer x) (car x))
	(define (denom x) (cdr x))
	```
- 为了显示计算结果，*将有理数打印为分式*，用斜线分割，procedure 如下（`display`  `newline` 为 primitive procedure）
	```scheme
	(define (print-rat x)
	  (newline)
	  (display (numer x))
	  (display "/")
	  (display (denom x)))
	```
- 示例：
	```scheme
	(define one-half (make-rat 1 2))
	(print-rat one-half)
	1/2
	(define one-third (make-rat 1 3))
	(print-rat (add-rat one-half one-third))
	5/6
	(print-rat (mul-rat one-half one-third))
	1/6
	(print-rat (add-rat one-third one-third))
	6/9
	```

### 简化分式
- 当前有理数实现并没有将有理数*约化到最简形式(reduce rational numbers to lowest terms)*。
- 通过修改 make-rat 很容易做到这件事。
- 参考 [[1.2.4 实例#最大公约数Greatest Common Divisors|gcd procedure]]，可求两个整数的最大公约数，可以利用它，在*构造序对(pairs)之前将分子和分母约化为最简单的项*：

	```scheme
	(define (make-rat n d)
	  (let ((g (gcd n d)))
	    (cons (/ n g) (/ d g))))
	```
- 就有：
	```scheme
	(print-rat (add-rat one-third one-third))
	2/3
	```

- 完成改动，*只需修改构造器 `make-rat`，* 不必修改任何实现（例如`add-rat`和`mul-rat`）。