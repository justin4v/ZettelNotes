#SICP #List
# Representing Sequences
- 利用 pairs 可以构造出有用的结构：序列 *sequence*
- 一系列数据对象的有序集：
![[序列机构示意.png]]
- 每个 pairs 的 *car* 部分对应于链中的一个元素，*cdr 是链中下一个 pairs*；
- 结尾用一个不是 pairs 的值表示，*表明 sequence 的结束，用 nil 变量表示*。
	```scheme
	(cons 1
	      (cons 2
	            (cons 3
	                  (cons 4 nil))))
	```
- **通过嵌套 cons 构造的、 pairs 的 sequence 称为一个表 list**。
- Lisp 提供了 primitive procedure *list*：*(list 1 2 3 4)*
- **(list ⟨a1⟩ ⟨a2⟩ . . . ⟨an⟩)**  等同于
```scheme
(cons <a1> 
		(cons <a2> 
				(cons ... 
						(cons <an> nil) 
								...)))
```
- Lisp 通常将 *sequence 以顺序打印，外面用括号括起*。上面打印为 *(1 2 3 4)*
	```scheme
	(define one-through-four (list 1 2 3 4))
	
	one-through-four
	#|打印结果|#
	(1 2 3 4)
	```

## 注意
- 区分 expression  `(list 1 2 3 4)` 和表list `(1 2 3 4)`.
- `(1 2 3 4)` 是表达式 `(list 1 2 3 4)`  求值的结果。
- 如果输入`(1 2 3 4)`，*解释器会认为其为表达式*， *认为第一个元素 `1` 是一个 procedure*。
- 然后解释器会试图将 procedure 1  apply 于参数 2、3和4，将会出错。

# List 操作
## 基础操作
- *car list* 选取表 *list 的第一项元素*；
- *cdr list* 选取表 *list 中除第一项外所有项形成的子表*；
- *cons* 可以*在 list 前增加元素*，list 结尾是 nil 所以 *list 后无法增加元素*；
- *nil* *表示序对 sequence 链的结束*，也可以表示不包含任何元素的序列。
	```scheme
	(car one-through-four)
	1
	
	(cdr one-through-four)
	(2 3 4)
	
	(car (cdr one-through-four))
	2
	
	(cons 10 one-through-four)
	(10 1 2 3 4)

	#|list 后面无法增加元素|#
	(cons 5 one-through-four)
	(5 1 2 3 4)
	```

## 遍历操作
### cdring down
1. 通过**顺序  “向下cdr (cdring down)”** list 的方式完成*对 list 的遍历*。
2. 例如：`list-ref` 参数是 `list` 和数 `n`，它返回 list 中的第 n 个项.
	1. 元素的编号从0开始；
	2. 对 n＝0，`list-ref` 返回 *(car list)* 项。
	3.  否则，`list-ref` 返回 *list' => (cdr list)* 的第（n－1）个项；
	```scheme
	(define (list-ref items n)
	  (if (= n 0)
	      (car items)
	      (list-ref (cdr items) (- n 1))))
	
	(define squares (list 1 4 9 16 25))
	
	(list-ref squares 3)
	16
	```
3. Scheme 提供了 primitive procedure  `null?` ：**用于检查表 list 是否为空表**。如返回 list 项数的 procedure `length`：
	```scheme
	(define (length items)
	  (if (null? items) 0
	      (+ 1 (length (cdr items)))))
	
	(define odds (list 1 3 5 7))
	
	(length odds)
	4
	```
	- length **递归方案**：
	- 任意 *list 的 length 等于 (cdr list) 的 length 加 1*；
	- 直到到达*基础情况： empty list => length 是 0*。
4. **迭代方案**设计 *length procedure*：
	 ```scheme
	(define (length items)
	  #|a is list count is start point|#
	  #|迭代每次记录完整状态，这里需要的就是 count(length)|#
	  (define (length-iter a count)
	    (if (null? a) count
	        (length-iter (cdr a) (+ 1 count))))
	  (length-iter items 0))
	```

### cons up
1. 另一常用程序设计技术是**在 *cdring down* 一个 list 的过程中 *cons up* 出结果表**；
2. 例如 procedure *append*，以两个 list 为参数，组合成一个新的 list：
	```scheme
	(append squares odds)
	(1 4 9 16 25 1 3 5 7)
	
	(append odds squares)
	(1 3 5 7 1 4 9 16 25)
	```
3. *append* 递归方案：
	- *如果 list1 为空，结果就是 list2*。
	- 否则*递归先得到子 list： list' => (cdr list1) ，得到 list‘ 和 list2 的 append 结果*；
	- *再将 (car list1) 通过 cons 增加到结果的前面*。
	```scheme
	(define (append list1 list2)
	       (if (null? list1) list2
	           (cons (car list1) (append (cdr list1) list2))))
	```
	- 每次都将问题分解（通过 `cdr` 和 `car`），然后解决子问题。

# Mapping over list
- **将某种变换应用于 list 的所有元素**，并得到所有结果构成的表，是一种常见的操作。
- 举例来说，下面 `scale-list`  将一个 list 里的所有元素按给定因子做一次缩放：

```lisp
(define (scale-list items factor)
	(if (null? items)
		nil
		(cons (* (car items) factor)
			(scale-list (cdr items)
							factor))))
						
(scale-list (list 1 2 3 4 5) 10)
(10 20 30 40 50)
```

^646ffb

## Map 抽象
- 可以抽象出具有一般性的想法，将其中的公共模式表述为一个高阶过程，称为map；
-  Map 有两个参数：一个 procedure 和 list，返回将 procedure 应用于 list 中各个元素得到的结果形成的 list。

```lisp
(define (map proc items)
	(if (null? items)
		nil
		(cons (proc (car items))
				(map proc (cdr items)))))
			
(map abs (list -10 2.5 -11.6 17))
(10 2.5 11.6 17)
(map (lambda (x) (* x x)) (list 1 2 3 4))
(1 4 9 16)
	
```

 - 用 `map` 重新定义 `scale-list` 
 
```lisp
(define (scale-list items factor)
	(map (lambda (x) (* x factor))
			items))
```

## Map 特点
- map 是一种很重要的结构，不仅因为它代表了一种公共模式，而且因为它建立起了一种处理表的高层抽象。
- 在  `scale-list` 原来的定义里，程序的*递归结构将注意力吸引到对于 list 中逐个元素的处理上*。
- 通过 map 定义 `scale-list` **抑制了这种细节**，强调的是*从原始 list 到结果 list 的一个缩放变换*。
- 这两种定义形式之间的差异，并不在于计算机会执行不同的计算过程〈其实不会），而在于**对同一个过程的不同思考方式**。
- 从作用上看，map 建起了一层**抽象屏障**，*将实现表变换的过程的实现，与如何提取表中元素以及组合结果的细节隔离开*。
- 与 [[2.1.2 抽象屏障#Abstraction Barriers|抽象屏障]]类似，map 抽象也提供了新的灵活性，有可能*在保持从序列到序列的变换操作框架的同时，改变序列实现的低层细节*。
